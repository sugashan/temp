plugins {
	id 'org.springframework.boot' version '2.2.0.RELEASE'
	id 'io.spring.dependency-management' version '1.0.8.RELEASE'
	id 'java'
}

group = 'com.sugashan.test'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '9'

repositories {
	mavenCentral()
}

sourceSets.main.java.srcDirs "src/generated-sources/java"
configurations {
	jaxb
}

dependencies {
	jaxb (
			'com.sun.xml.bind:jaxb-xjc:2.2.7',
			'com.sun.xml.bind:jaxb-impl:2.2.7'
	)
	implementation 'javax.xml.bind:jaxb-api:2.3.1'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.boot:spring-boot-starter-web-services'
	testImplementation('org.springframework.boot:spring-boot-starter-test') {
		exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
	}
}

test {
	useJUnitPlatform()
}

task jaxb {
	System.setProperty('javax.xml.accessExternalSchema', 'all')
	def jaxbTargetDir = file("src/generated-sources/java")
	doLast {
		jaxbTargetDir.mkdirs()
		ant.taskdef(
				name: 'xjc',
				classname: 'com.sun.tools.xjc.XJCTask',
				classpath: configurations.jaxb.asPath
		)
		ant.jaxbTargetDir = jaxbTargetDir
		ant.xjc(
				destdir: '${jaxbTargetDir}',
				package: 'com.sugashan.test.gen',
				schema: 'src/main/resources/xsd/CustomerOrdersRequest.xsd'
		)
	}
}
compileJava.dependsOn jaxb
